# ZSH configuration file 

# Add a path to $PATH
function add_to_path {
	export PATH="$1:$PATH"
}

# Add paths
add_to_path "$HOME/.local/bin/"
add_to_path "$HOME/.cargo/bin/"

# Less settings for nice color output
export LESS_ADVANCED_PREPROCESSOR=1
export LESS=-R
export LESS_TERMCAP_mb=$'\E[1;31m'
export LESS_TERMCAP_md=$'\E[1;36m'
export LESS_TERMCAP_me=$'\E[0m'
export LESS_TERMCAP_se=$'\E[0m'
export LESS_TERMCAP_so=$'\E[01;44;33m'
export LESS_TERMCAP_ue=$'\E[0m'

# Setup editor and language
export EDITOR=vim
export LANG=en_US.UTF-8

# Restic backup repository settings
export RESTIC_REPOSITORY=sftp:ale@alerighi.duckdns.org:/storage/backups
export RESTIC_PASSWORD=ale96

# Setup a bunch of options
setopt noclobber  	# do not accidentaly truncate existing files with >
setopt ignoreeof  	# do not terminate on CTRL-D
setopt correct    	# autocorrect commands
setopt autocd     	# automaticaly cd typing the name of the directory
setopt completealiases	# completion for aliases
setopt noautopushd	# do not automatically pushd
setopt histignoredups	# ignore history duplicates
setopt appendhistory	# append to history
setopt sharehistory     # share history across terminals
setopt incappendhistory # append history immediately
setopt extendedglob 	# extended globs
setopt notify

# Aliases 
alias ls="ls --color"
alias ll="ls -lhF"
alias la="ls -lAhF"
alias grep="grep --color=always"
alias du="du -h"	# human readable is better
alias dig="drill" 	# old dig command is deprecated 
alias open="xdg-open"	# I'm used to a Mac... 

# Enable the command run-help to get documentation for builtins
unalias run-help
autoload run-help
alias help="run-help"   # compatibility with bash help

# Enable completion 
autoload -Uz compinit && compinit

# Compatibility with bash autocomplete
# autoload -U bashcompinit && bashcompinit

# Use LS_COLORS for completion
eval $(dircolors)
zstyle ':completion:*:default' list-colors ${(s.:.)LS_COLORS}

# Use menu for completion 
zstyle ':completion:*' menu select

# Completion for kill command
zstyle ':completion:*:*:kill:*:processes' list-colors '=(#b) #([0-9]#)*=0=01;31'
zstyle ':completion:*:kill:*' command 'ps -u $USER -o pid,ucomm,comm'

# Rehash on completion, useful to get completions when you install new software
zstyle ':completion:*:commands' rehash 1

# History settings
HISTFILE=~/.zsh_history
HISTSIZE=10000
SAVEHIST=10000

# Setup git prompt 
ZSH_THEME_GIT_PROMPT_CACHE=1 # use cache to improve performance
source $HOME/.zsh/zsh-git-prompt/zshrc.sh

# Setup prompt 
NL=$'\n'
PROMPT="%F{green}%n@%m%F{magenta} :: %F{cyan}%~%f%b \$(git_super_status)$NL %F{yellow}::%f "

# Bind keys in emacs mode
bindkey -e

# To make delete key working. I use emacs CTRL-D anyway
# bindkey "^[[3~"  delete-char
# bindkey "^[3;5~" delete-char

# Avoid that programs modify the terminal state
ttyctl -f

# Function that gets executed before a command is executed
function preexec {
	_last_command_start_time=$SECONDS
}

# Function that gets executed before the prompt is displayed
function precmd {	
	local return_value=$? hours minutes seconds execution_time command_status="%F{green} ok "

	if [[ -n $_last_command_start_time ]]; then
		execution_time=$((SECONDS - _last_command_start_time))
		unset _last_command_start_time
		
		if ((return_value != 0)); then
			command_status="%F{red} fail %F{blue}status %F{yellow}$return_value "
		fi

		if ((execution_time >= 3600)); then
			hours=" $((execution_time / 3600))h"
		fi
	
		if ((execution_time >= 60)); then 
			minutes=" $((execution_time % 3600 / 60))m"
		fi

		if ((execution_time > 1)); then
			seconds=" $((execution_time % 60))s"
		fi	
	fi	
	
	# Set right prompt
	RPROMPT="%F{blue}[%F{magenta}$hours$minutes$seconds$command_status%F{blue}]%f"

	# Set apple terminal window title
	print -Pn "\e]2;%n@%M %~\a"
}

# Syntax highlight
source ~/.zsh/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
